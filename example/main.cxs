Strict

Import mojo2
Import fileobject

' this is very important as it enables reflection in this file
' main.cxs is the name of this file, where the test_obj class is
#REFLECTION_FILTER="main"


' a very simple object with different types
Class test_obj
	Field number:Int
	Field floating:Float
	Field text:String
	
	Field img:Image
	
'	Field n:nested[2]
	Field b:Bool
	Field ba:Bool[4]
		
	Field arr1:Int[10]
	Field arr2:Float[10]
	Field arr3:String[10]
	
	Field nest1:nested
	Field nest2:nested
End

Class nested
	Field num:Int
	Field arr:Float[2]
End

Class myClass Extends App
	Field canvas:Canvas
	Field test:test_obj
	
	Method OnCreate:Int()
		SetUpdateRate(60)				
		canvas = New Canvas

		' create an instance of test_obj		
		test = New test_obj()
		test.number = 36485
		test.text = "WAS IT A CAR OR A CAT I SAW"
		test.floating = 7.8249
		test.arr1[5]=12345
		test.b = True
		test.ba[2] = True
		test.ba[3] = False
		test.nest1 = New nested()
		test.nest2 = New nested()
'		test.n[0] = New nested()
'		test.n[1] = New nested()
		' auto save the object
		SaveObject("test.txt",test)
		
		Return 0
	End

	Method OnUpdate:Int()
		If KeyHit(KEY_D) Then DisplayObject(test)
		If KeyHit(KEY_S) Then SaveObject("test.txt",test)
		If KeyHit(KEY_L) Then LoadObject("test.txt",test)
		If KeyHit(KEY_O) Then OpenGameState()
		If KeyHit(KEY_W) Then WriteGameState()

		If KeyHit(KEY_C)
			test.number = 0
			test.floating = 0
			test.text = "" 
		End
		
		Return 0
	End
	
	Method OnRender:Int()
		canvas.Clear (0,0.5,0)	
		
		canvas.DrawText "dynamic object files test",20,0
		
		canvas.DrawText "number="+test.number,20,60
		canvas.DrawText "floating="+test.floating,20,80
		canvas.DrawText "text="+test.text,20,100

		canvas.DrawText "S-save object      L-load object    C-clear object   D-display object",20,300
		canvas.DrawText "O-Open GameState   W-Write GameState",20,320
		canvas.Flush

		Return 0
	End
	
	Method WriteGameState:Void()
		Local data:String[] = Object2String(test)
		Local text:String = ""
		Local i:Int
		While i<data.Length()
			text+=data[i]+"##"
			i+=1
		Wend
		SaveState(text)
	End
	
	Method OpenGameState:Void()
		Local text:String = LoadState()
		Local data:String[] = text.Split("##")
		
		String2Object(data,test)
	End
End

Function Main:Int()
	New myClass		
	Return 0
End

